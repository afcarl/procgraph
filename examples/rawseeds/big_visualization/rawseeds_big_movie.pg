--- model rawseeds_big_movie
import procgraph.components.rawseeds 
import procgraph.components.cv 
import procgraph.components.pil

hokuyo_front.file = "${logdir}/HOKUYO_FRONT.csv.bz2"
 hokuyo_rear.file = "${logdir}/HOKUYO_REAR.csv.bz2"
  sick_front.file = "${logdir}/SICK_FRONT.csv.bz2"
   sick_rear.file = "${logdir}/SICK_REAR.csv.bz2"

       svs_t.dir = "${logdir}/SVS_T"
       svs_r.dir = "${logdir}/SVS_R"
       svs_l.dir = "${logdir}/SVS_L"
        omni.dir = "${logdir}/OMNI"
     frontal.dir = "${logdir}/FRONTAL"

             out = "${logdir}/rawseeds_big_movie.avi"


|svs_t:RawseedsCam| --> |svs_pipeline| --> svs_t
|svs_r:RawseedsCam| --> |svs_pipeline| --> svs_r
|svs_l:RawseedsCam| --> |svs_pipeline| --> svs_l

|frontal:RawseedsCam| --> |resize width=320 height=240| --> frontal
   |omni:RawseedsCam| --> |resize width=320 height=320| --> omni

|hokuyo_front:RawseedsHokuyo| --> hokuyo_front
| hokuyo_rear:RawseedsHokuyo| --> hokuyo_rear
|  sick_front:RawseedsRF|     --> sick_front
|   sick_rear:RawseedsRF|     --> sick_rear

# display all laser data together
hokuyo_rear, hokuyo_front, sick_front, sick_rear --> |sync| --> |join| --> y --> |rgbplot width=640 height=240| --> display_laser

# synchronize everything to the frontal camera
# frontal, svs_t, svs_r, svs_l, omni, display_laser --> |S:sync|
frontal, svs_t, svs_r, svs_l, omni, display_laser --> |S:identity|

# compose everything together
S.svs_t, S.svs_r, S.svs_l, S.omni, S.frontal, S.display_laser --> |compose width=1280 height=768| --> |text| --> rgb
    compose.positions = {svs_t: [0,0], svs_l: [0,240], svs_r: [0,480],  omni: [320,0], frontal: [320,320], display_laser: [640,0]}
    
rgb --> |mencoder file=$out fps=30|


# coordinates for video

text.texts = [  {string: "raw image", position: [10,10], size:20, halign: left, color: black, bg: white  }, {string: "processed image", position: [330,10], size:20, halign: left, color: "#ff00ff", bg: white  }]


--- model svs_pipeline
svsw = 320
svsh = 240
|input name=cam| --> |gray2rgb| -->  |resize width=$svsw height=$svsh| --> |output name=rgb|


